plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.30'
    id "org.jlleitschuh.gradle.ktlint" version '7.2.1'
    id "com.adarshr.test-logger" version "1.6.0" apply false
}

allprojects {
    group = 'account-tx'

    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: "com.adarshr.test-logger"

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
        implementation "io.vertx:vertx-web:$vertxVersion"
        implementation "io.vertx:vertx-lang-kotlin:$vertxVersion"
        implementation "io.vertx:vertx-lang-kotlin-coroutines:$vertxVersion"
        implementation "com.google.code.gson:gson:$gsonVersion"

        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
        testImplementation "io.vertx:vertx-web-client:$vertxVersion"
        testImplementation "io.vertx:vertx-junit5:$vertxVersion"
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            preferProjectModules()
        }
    }

    testlogger {
        theme 'standard-parallel'
        showPassed true
        showSkipped true
        showFailed true
    }

    test {
        useJUnitPlatform()
        failFast = true

        testLogging {
            showStandardStreams = true
        }
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = sourceCompatibility

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = targetCompatibility
            allWarningsAsErrors = true
        }
    }
}
